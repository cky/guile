dnl   configuration script for Guile
dnl   Process this file with autoconf to produce configure.
dnl
dnl  Copyright (C) 1998, 1999, 2000, 2001, 2002 Free Software Foundation, Inc.
dnl
dnl  This file is part of GUILE
dnl
dnl  GUILE is free software; you can redistribute it and/or modify it
dnl  under the terms of the GNU General Public License as published by
dnl  the Free Software Foundation; either version 2, or (at your
dnl  option) any later version.
dnl
dnl  GUILE is distributed in the hope that it will be useful, but
dnl  WITHOUT ANY WARRANTY; without even the implied warranty of
dnl  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl  General Public License for more details.
dnl
dnl  You should have received a copy of the GNU General Public License
dnl  along with GUILE; see the file COPYING.  If not, write to the
dnl  Free Software Foundation, Inc., 59 Temple Place - Suite 330,
dnl  Boston, MA 02111-1307, USA.

AC_PREREQ(2.53)

AC_INIT
AC_CONFIG_SRCDIR([Makefile.in])
. $srcdir/GUILE-VERSION
AM_INIT_AUTOMAKE($PACKAGE, $VERSION, no-define)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(libguile/scmconfig.h)

#--------------------------------------------------------------------
#
# Independent Subdirectories
#
#--------------------------------------------------------------------

AC_CONFIG_SUBDIRS(guile-readline)

#--------------------------------------------------------------------
#
# User options
#
#--------------------------------------------------------------------

GUILE_ERROR_ON_WARNING="yes"

AC_ARG_ENABLE(error-on-warning,
  [  --enable-error-on-warning    treat compile warnings as errors],
  [case "${enableval}" in
     yes | y) GUILE_ERROR_ON_WARNING="yes" ;;
     no | n)  GUILE_ERROR_ON_WARNING="no" ;;
     *) AC_MSG_ERROR(bad value ${enableval} for --enable-error-on-warning) ;;
   esac])

AC_ARG_ENABLE(debug-freelist,
  [  --enable-debug-freelist include garbage collector freelist debugging code],
  if test "$enable_debug_freelist" = y || test "$enable_debug_freelist" = yes; then
    AC_DEFINE(GUILE_DEBUG_FREELIST, 1,
      [Define this if you want to debug the free list (helps w/ GC bugs).])
  fi)

AC_ARG_ENABLE(debug-malloc,
  [  --enable-debug-malloc   include malloc debugging code],
  if test "$enable_debug_malloc" = y || test "$enable_debug_malloc" = yes; then
    AC_DEFINE(GUILE_DEBUG_MALLOC, 1,
      [Define this if you want to debug scm_must_malloc/realloc/free calls.])
  fi)

AC_ARG_ENABLE(guile-debug,
  [  --enable-guile-debug    include internal debugging functions],
  if test "$enable_guile_debug" = y || test "$enable_guile_debug" = yes; then
    AC_DEFINE(GUILE_DEBUG, 1,
      [Define this to include various undocumented functions used to debug.])
  fi)

AC_ARG_ENABLE(arrays,
  [  --disable-arrays        omit array and uniform array support],,
  enable_arrays=yes)

AC_ARG_ENABLE(posix,
  [  --disable-posix         omit posix interfaces],,
  enable_posix=yes)

AC_ARG_ENABLE(networking,
  [  --disable-networking    omit networking interfaces],,
  enable_networking=yes)

AC_ARG_ENABLE(regex,
  [  --disable-regex         omit regular expression interfaces],,
  enable_regex=yes)

AC_ARG_ENABLE(htmldoc,
  [  --enable-htmldoc        build HTML documentation as well as Info],
  [if test "$enable_htmldoc" = "" || test "$enable_htmldoc" = y || test "$enable_htmldoc" = yes; then
     htmldoc_enabled=yes
     AC_PATH_PROG(TEXI2HTML, texi2html, not found)
     if test "$TEXI2HTML" = "not found"; then
       echo
       echo Building HTML documentation requires the \`texi2html\' program,
       echo which appears not to be present on your machine.
       echo
       echo \`texi2html\' is available from
       echo 'http://www.mathematik.uni-kl.de/~obachman/Texi2html/.'
       echo
       echo In the meantime, to build the guile-doc distribution
       echo without HTML enabled, please rerun \`./configure\' without
       echo the \`--enable-htmldoc\' option.
       exit -1
     fi
   fi])

AM_CONDITIONAL(HTMLDOC, test x$htmldoc_enabled = xyes)

AC_ARG_ENABLE(deprecated,
  [  --disable-deprecated    omit deprecated features])

AH_TEMPLATE([SCM_ENABLE_DEPRECATED],
  [Define this to 1 if you want to include deprecated features.])

if test "$enable_deprecated" = no; then
  AC_DEFINE(SCM_ENABLE_DEPRECATED, 0)
else
  if test "$enable_deprecated" = yes || test "$enable_deprecated" = ""; then
    warn_default=summary
  elif test "$enable_deprecated" = shutup; then
    warn_default=no
  else
    warn_default=$enable_deprecated
  fi
  AC_DEFINE(SCM_ENABLE_DEPRECATED, 1)
  AC_DEFINE_UNQUOTED(SCM_WARN_DEPRECATED_DEFAULT, "$warn_default",
  [Define this to control the default warning level for deprecated features.])
fi

dnl The --disable-debug used to control these two.  But now they are
dnl a required part of the distribution.
AC_DEFINE(DEBUG_EXTENSIONS, 1,
  [Define if you want support for debugging Scheme programs.])
AC_DEFINE(READER_EXTENSIONS, 1,
  [Define if you want support for debugging Scheme programs.])

AC_ARG_ENABLE(elisp,
  [  --disable-elisp         omit Emacs Lisp support],,
  enable_elisp=yes)

#--------------------------------------------------------------------

dnl Some more checks for Win32
AC_CYGWIN
AC_MINGW32
AC_LIBTOOL_WIN32_DLL

AC_PROG_INSTALL
AC_PROG_CC
AC_PROG_CPP
AC_PROG_AWK

AC_AIX
AC_ISC_POSIX
AC_MINIX

AM_PROG_CC_STDC

AC_LIBTOOL_DLOPEN
AM_PROG_LIBTOOL
AC_LIB_LTDL

AC_CHECK_PROG(have_makeinfo, makeinfo, yes, no)
AM_CONDITIONAL(HAVE_MAKEINFO, test "$have_makeinfo" = yes)

dnl Check for dynamic linking

use_modules=yes
AC_ARG_WITH(modules,
[  --with-modules[=FILES]  Add support for dynamic modules],
use_modules="$withval")
test -z "$use_modules" && use_modules=yes
DLPREOPEN=
if test "$use_modules" != no; then
  AC_DEFINE(DYNAMIC_LINKING, 1,
    [Define if you want support for dynamic linking.])
  if test "$use_modules" = yes; then
    DLPREOPEN="-dlpreopen force"
  else
    DLPREOPEN="-export-dynamic"
    for module in $use_modules; do
      DLPREOPEN="$DLPREOPEN -dlopen $module"
    done
  fi
fi

dnl files which are destined for separate modules.

if test "$use_modules" != no; then
   AC_LIBOBJ([dynl])
fi

if test "$enable_arrays" = yes; then
   AC_LIBOBJ([ramap])
   AC_LIBOBJ([unif])
   AC_DEFINE(HAVE_ARRAYS, 1,
     [Define this if you want support for arrays and uniform arrays.])
fi

if test "$enable_posix" = yes; then
   AC_LIBOBJ([filesys])
   AC_LIBOBJ([posix])
   AC_DEFINE(HAVE_POSIX, 1,
     [Define this if you want support for POSIX system calls in Guile.])
fi

if test "$enable_networking" = yes; then
   AC_LIBOBJ([net_db])
   AC_LIBOBJ([socket])
   AC_DEFINE(HAVE_NETWORKING, 1,
     [Define this if you want support for networking in Guile.])
fi

if test "$enable_debug_malloc" = yes; then
   AC_LIBOBJ([debug-malloc])
fi

if test "$enable_elisp" = yes; then
   AC_DEFINE(SCM_ENABLE_ELISP, 1,
     [Define this if you want Elisp support (in addition to Scheme).])
fi


AC_C_CONST
AC_C_INLINE
AC_C_BIGENDIAN

if test "$ac_cv_c_inline" != no; then
  AC_DEFINE(HAVE_INLINE, 1,
    [Define if the compiler supports inline functions.])
fi

AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(size_t)

dnl Check for integral types that can represent the range of pointers.
dnl If these types don't exist on this platform, they are replaced by
dnl "unsigned long" and "long", respectively.

AC_CHECK_HEADERS(stdint.h)
AC_CHECK_HEADERS(inttypes.h)
AC_CHECK_SIZEOF(uintptr_t)
AC_CHECK_SIZEOF(ptrdiff_t)

AC_CHECK_SIZEOF(long long)

AC_CHECK_SIZEOF(void *)

if test "$ac_cv_sizeof_long" -ne "$ac_cv_sizeof_void_p"; then
  AC_MSG_ERROR(sizes of long and void* are not identical)
fi

AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_TIME
AC_HEADER_SYS_WAIT

AC_CHECK_HEADERS([io.h libc.h limits.h malloc.h memory.h string.h \
regex.h rxposix.h rx/rxposix.h sys/dir.h sys/ioctl.h sys/select.h \
sys/time.h sys/timeb.h sys/times.h sys/stdtypes.h sys/types.h \
sys/utime.h time.h unistd.h utime.h pwd.h grp.h sys/utsname.h \
direct.h])

GUILE_HEADER_LIBC_WITH_UNISTD

AC_TYPE_GETGROUPS
AC_TYPE_SIGNAL
AC_TYPE_MODE_T

AC_CHECK_LIB(m, main)
AC_CHECK_FUNCS(gethostbyname)
if test $ac_cv_func_gethostbyname = no; then
    AC_CHECK_LIB(nsl, gethostbyname)
fi

AC_CHECK_FUNCS(connect)
if test $ac_cv_func_connect = no; then
    AC_CHECK_LIB(socket, connect)
fi

dnl
dnl Check for Winsock and other functionality on Win32 (*not* CygWin)
dnl
EXTRA_DEFS=""
if test "$MINGW32" = "yes" ; then
    AC_CHECK_HEADER(winsock2.h, [AC_DEFINE([HAVE_WINSOCK2_H], 1,
      [Define if you have the <winsock2.h> header file.])])
    AC_CHECK_LIB(ws2_32, main)
    AC_LIBOBJ([win32-uname])
    AC_LIBOBJ([win32-dirent])
    if test "$enable_networking" = yes ; then
      AC_LIBOBJ([win32-socket])
    fi
    if test "$enable_shared" = yes ; then
      EXTRA_DEFS="-DSCM_IMPORT"
      AC_DEFINE(USE_DLL_IMPORT, 1,
        [Define if you need additional CPP macros on Win32 platforms.])
    fi
    if test x"$enable_ltdl_install" = x"yes" ; then
      INCLTDL="-DLIBLTDL_DLL_IMPORT $INCLTDL"
    fi
fi
AC_SUBST(EXTRA_DEFS)

# FIXME: check to see if we still need these.
#AC_SUBST(INCLTDL)
#AC_SUBST(LIBLTDL)

AC_SUBST(DLPREOPEN)

AC_CHECK_HEADERS([assert.h])

AC_CHECK_FUNCS([ctermid ftime fchown getcwd geteuid gettimeofday lstat mkdir mknod nice readlink rename rmdir select setegid seteuid setlocale setpgid setsid sigaction siginterrupt strftime strptime symlink sync tcgetpgrp tcsetpgrp times uname waitpid strdup system usleep atexit on_exit chown link fcntl ttyname getpwent getgrent kill getppid getpgrp fork setitimer getitimer strchr strcmp index bcopy memcpy rindex])

AC_CHECK_HEADERS(crypt.h sys/resource.h sys/file.h)
AC_CHECK_FUNCS(chroot flock getlogin cuserid getpriority setpriority getpass sethostname gethostname)
AC_CHECK_LIB(crypt, crypt)

### Some systems don't declare some functions.  On such systems, we
### need to at least provide our own K&R-style declarations.

### GUILE_FUNC_DECLARED(function, headerfile)

### Check for a declaration of FUNCTION in HEADERFILE; if it is
### not there, #define MISSING_FUNCTION_DECL.
AC_DEFUN(GUILE_FUNC_DECLARED, [
  AC_CACHE_CHECK(for $1 declaration, guile_cv_func_$1_declared,
    AC_EGREP_HEADER($1, $2,
		    guile_cv_func_$1_declared=yes,
		    guile_cv_func_$1_declared=no))
  if test [x$guile_cv_func_]$1[_declared] = xno; then
    AC_DEFINE([MISSING_]translit($1, [a-z], [A-Z])[_DECL], 1,
      [Define if the operating system supplies $1 without declaring it.])
  fi
])

GUILE_FUNC_DECLARED(strptime, time.h)
GUILE_FUNC_DECLARED(sleep, unistd.h)
GUILE_FUNC_DECLARED(usleep, unistd.h)

### On some systems usleep has no return value.  If it does have one,
### we'd like to return it; otherwise, we'll fake it.
AC_CACHE_CHECK([return type of usleep], guile_cv_func_usleep_return_type,
  [AC_EGREP_HEADER(changequote(<, >)<void[ 	]+usleep>changequote([, ]),
                   unistd.h,
		   [guile_cv_func_usleep_return_type=void],
		   [guile_cv_func_usleep_return_type=int])])
case "$guile_cv_func_usleep_return_type" in
  "void" )
    AC_DEFINE(USLEEP_RETURNS_VOID, 1,
      [Define if the system headers declare usleep to return void.])
  ;;
esac

AC_CHECK_HEADER(sys/un.h, have_sys_un_h=1)
if test -n "$have_sys_un_h" ; then
  AC_DEFINE(HAVE_UNIX_DOMAIN_SOCKETS, 1,
    [Define if the system supports Unix-domain (file-domain) sockets.])
fi

AC_CHECK_FUNCS(socketpair getgroups setpwent pause tzset)

AC_CHECK_FUNCS(sethostent   gethostent   endhostent   dnl
               setnetent    getnetent    endnetent    dnl
               setprotoent  getprotoent  endprotoent  dnl
               setservent   getservent   endservent   dnl
               getnetbyaddr getnetbyname dnl
	       inet_lnaof inet_makeaddr inet_netof hstrerror dnl
	       inet_pton inet_ntop)

AC_MSG_CHECKING(for __libc_stack_end)
AC_TRY_LINK([extern char *__libc_stack_end;],
            [char *p = __libc_stack_end;],
            have_libc_stack_end=yes,
            have_libc_stack_end=no)
AC_MSG_RESULT($have_libc_stack_end)

if test $have_libc_stack_end = yes; then
  AC_DEFINE(HAVE_LIBC_STACK_END, 1,
           [Define if you have the __libc_stack_end variable.])
fi

dnl Some systems do not declare this.  Some systems do declare it, as a
dnl macro.  With cygwin it may be in a DLL.

AC_MSG_CHECKING(whether netdb.h declares h_errno)
AC_CACHE_VAL(guile_cv_have_h_errno,
[AC_TRY_COMPILE([#include <netdb.h>],
[int a = h_errno;],
guile_cv_have_h_errno=yes, guile_cv_have_h_errno=no)])
AC_MSG_RESULT($guile_cv_have_h_errno)
if test $guile_cv_have_h_errno = yes; then
  AC_DEFINE(HAVE_H_ERRNO, 1, [Define if h_errno is declared in netdb.h.])
fi

AC_MSG_CHECKING(whether uint32_t is defined)
AC_CACHE_VAL(guile_cv_have_uint32_t,
  [AC_TRY_COMPILE([#include <sys/types.h>
		   #if HAVE_STDINT_H
		   #include <stdint.h>
                   #endif
		   #ifndef __MINGW32__
	           #include <netdb.h>
		   #endif],
		  [uint32_t a;],
		  guile_cv_have_uint32_t=yes, guile_cv_have_uint32_t=no)])
AC_MSG_RESULT($guile_cv_have_uint32_t)
if test $guile_cv_have_uint32_t = yes; then
  AC_DEFINE(HAVE_UINT32_T, 1,
    [Define if uint32_t typedef is defined when netdb.h is include.])
fi

AC_MSG_CHECKING(for working IPv6 support)
AC_CACHE_VAL(guile_cv_have_ipv6,
[AC_TRY_COMPILE([
#ifdef HAVE_SYS_TYPES_H
#include <sys/types.h>
#endif
#include <netinet/in.h>
#include <sys/socket.h>],
[struct sockaddr_in6 a;  a.sin6_family = AF_INET6;],
guile_cv_have_ipv6=yes, guile_cv_have_ipv6=no)])
AC_MSG_RESULT($guile_cv_have_ipv6)
if test $guile_cv_have_ipv6 = yes; then
  AC_DEFINE(HAVE_IPV6, 1, [Define if you want support for IPv6.])
fi

# included in rfc2553 but not in older implementations, e.g., glibc 2.1.3.
AC_MSG_CHECKING(whether sockaddr_in6 has sin6_scope_id)
AC_CACHE_VAL(guile_cv_have_sin6_scope_id,
[AC_TRY_COMPILE([
#ifdef HAVE_SYS_TYPES_H
#include <sys/types.h>
#endif
#include <netinet/in.h>],
[struct sockaddr_in6 sok;  sok.sin6_scope_id = 0;],
guile_cv_have_sin6_scope_id=yes, guile_cv_have_sin6_scope_id=no)])
AC_MSG_RESULT($guile_cv_have_sin6_scope_id)
if test $guile_cv_have_sin6_scope_id = yes; then
  AC_DEFINE(HAVE_SIN6_SCOPE_ID, 1,
    [Define this if your IPv6 has sin6_scope_id in sockaddr_in6 struct.])
fi

AC_MSG_CHECKING(whether localtime caches TZ)
AC_CACHE_VAL(guile_cv_localtime_cache,
[if test x$ac_cv_func_tzset = xyes; then
AC_TRY_RUN([#include <time.h>
#if STDC_HEADERS
# include <stdlib.h>
#endif
extern char **environ;
unset_TZ ()
{
  char **from, **to;
  for (to = from = environ; (*to = *from); from++)
    if (! (to[0][0] == 'T' && to[0][1] == 'Z' && to[0][2] == '='))
      to++;
}
char TZ_GMT0[] = "TZ=GMT0";
char TZ_PST8[] = "TZ=PST8";
main()
{
  time_t now = time ((time_t *) 0);
  int hour_GMT0, hour_unset;
  if (putenv (TZ_GMT0) != 0)
    exit (1);
  hour_GMT0 = localtime (&now)->tm_hour;
  unset_TZ ();
  hour_unset = localtime (&now)->tm_hour;
  if (putenv (TZ_PST8) != 0)
    exit (1);
  if (localtime (&now)->tm_hour == hour_GMT0)
    exit (1);
  unset_TZ ();
  if (localtime (&now)->tm_hour != hour_unset)
    exit (1);
  exit (0);
}], guile_cv_localtime_cache=no, guile_cv_localtime_cache=yes,
[# If we have tzset, assume the worst when cross-compiling.
guile_cv_localtime_cache=yes])
else
        # If we lack tzset, report that localtime does not cache TZ,
        # since we can't invalidate the cache if we don't have tzset.
        guile_cv_localtime_cache=no
fi])dnl
AC_MSG_RESULT($guile_cv_localtime_cache)
if test $guile_cv_localtime_cache = yes; then
  AC_DEFINE(LOCALTIME_CACHE, 1, [Define if localtime caches the TZ setting.])
fi

dnl Test whether system calls are restartable by default on the
dnl current system.  If they are not, we put a loop around every system
dnl call to check for EINTR (see SCM_SYSCALL) and do not attempt to
dnl change from the default behaviour.  On the other hand, if signals
dnl are restartable then the loop is not installed and when libguile
dnl initialises it also resets the behaviour of each signal to cause a
dnl restart (in case a different runtime had a different default
dnl behaviour for some reason: e.g., different versions of linux seem
dnl to behave differently.)

AC_SYS_RESTARTABLE_SYSCALLS

if test "$enable_regex" = yes; then
   if test "$ac_cv_header_regex_h" = yes ||
      test "$ac_cv_header_rxposix_h" = yes ||
      test "$ac_cv_header_rx_rxposix_h" = yes; then
     GUILE_NAMED_CHECK_FUNC(regcomp, norx, [AC_LIBOBJ([regex-posix])],
     [AC_CHECK_LIB(rx, main)
      GUILE_NAMED_CHECK_FUNC(regcomp, rx, [AC_LIBOBJ([regex-posix])],
     [AC_CHECK_LIB(regex, main)
      GUILE_NAMED_CHECK_FUNC(regcomp, regex, [AC_LIBOBJ([regex-posix])])])]
     )
     dnl The following should not be necessary, but for some reason
     dnl autoheader misses it if we don't include it!
     if test "$ac_cv_func_regcomp_norx" = yes ||
        test "$ac_cv_func_regcomp_regex" = yes ||
        test "$ac_cv_func_regcomp_rx" = yes; then
       AC_DEFINE(HAVE_REGCOMP, 1,
         [This is included as part of a workaround for a autoheader bug.])
     fi
   fi
fi

AC_REPLACE_FUNCS(inet_aton putenv strerror memmove mkstemp)

AC_CHECK_HEADERS(floatingpoint.h ieeefp.h nan.h)

AC_CHECK_FUNCS(finite isinf isnan copysign)

# When testing for the presence of alloca, we need to add alloca.o
# explicitly to LIBOBJS to make sure that it is translated to
# `alloca.lo' for libtool later on.  This can and should be done more cleanly.
AC_FUNC_ALLOCA
if test "$ALLOCA" = "alloca.o"; then AC_LIBOBJ([alloca]); fi

AC_CHECK_MEMBERS([struct stat.st_rdev])
AC_CHECK_MEMBERS([struct stat.st_blksize])

AC_STRUCT_ST_BLOCKS

AC_CACHE_CHECK([for S_ISLNK in sys/stat.h], ac_cv_macro_S_ISLNK,
  [AC_TRY_CPP([#include <sys/stat.h>
               #ifndef S_ISLNK
               #error no S_ISLNK
               #endif],
               ac_cv_macro_S_ISLNK=yes,
               ac_cv_macro_S_ISLNK=no)])
if test $ac_cv_macro_S_ISLNK = yes; then
  AC_DEFINE(HAVE_S_ISLNK, 1,
    [Define this if your system defines S_ISLNK in sys/stat.h.])
fi

AC_STRUCT_TIMEZONE
GUILE_STRUCT_UTIMBUF

#--------------------------------------------------------------------
#
# Which way does the stack grow?
#
#--------------------------------------------------------------------

AC_TRY_RUN(aux (l) unsigned long l;
	     { int x; exit (l >= ((unsigned long)&x)); }
	   main () { int q; aux((unsigned long)&q); },
	   [AC_DEFINE([SCM_STACK_GROWS_UP], 1,
              [Define this if a callee's stack frame has a higher address
               than the caller's stack frame.  On most machines, this is
               not the case.])],
	   [],
           [AC_MSG_WARN(Guessing that stack grows down -- see scmconfig.h.in)])

AH_TEMPLATE([SCM_SINGLES],
  [Define this if floats are the same size as longs.])

AC_CACHE_CHECK([whether floats fit in longs], guile_cv_type_float_fits_long,
    [AC_TRY_RUN([main () { exit (sizeof(float) > sizeof(long)); }],
		[guile_cv_type_float_fits_long=yes],
		[guile_cv_type_float_fits_long=no],
		[guile_cv_type_float_fits_long=guess-yes])])
case $guile_cv_type_float_fits_long in
  "yes" )
    AC_DEFINE(SCM_SINGLES)
  ;;
  "guess-yes" )
    AC_DEFINE(SCM_SINGLES)
    AC_MSG_WARN([guessing that sizeof(long) == sizeof(float)])
    AC_MSG_WARN([see SCM_SINGLES in scmconfig.h.in])
  ;;
esac


AC_MSG_CHECKING(for struct linger)
AC_CACHE_VAL(scm_cv_struct_linger,
	AC_TRY_COMPILE([
#include <sys/types.h>
#include <sys/socket.h>],
			[struct linger lgr;  lgr.l_linger = 100],
			scm_cv_struct_linger="yes",
			scm_cv_struct_linger="no"))
AC_MSG_RESULT($scm_cv_struct_linger)
if test $scm_cv_struct_linger = yes; then
	AC_DEFINE(HAVE_STRUCT_LINGER, 1,
          [Define this if your system defines struct linger, for use with the
           getsockopt and setsockopt system calls.])
fi


AC_MSG_CHECKING(for struct timespec)
AC_CACHE_VAL(scm_cv_struct_timespec,
	AC_TRY_COMPILE([
#include <time.h>],
			[struct timespec t;  t.tv_nsec = 100],
			scm_cv_struct_timespec="yes",
			scm_cv_struct_timespec="no"))
AC_MSG_RESULT($scm_cv_struct_timespec)
if test $scm_cv_struct_timespec = yes; then
  AC_DEFINE(HAVE_STRUCT_TIMESPEC, 1,
    [Define this if your system defines struct timespec via <time.h>.])
fi

#--------------------------------------------------------------------
#
# Flags for thread support
#
#--------------------------------------------------------------------

### What thread package has the user asked for?
AC_ARG_WITH(threads, [  --with-threads          thread interface],
            , with_threads=no)

### Turn $with_threads into either the name of a threads package, like
### `qt', or `no', meaning that threads should not be supported.
AC_MSG_CHECKING(what kind of threads to support)
case "$with_threads" in
  "yes" | "qt" | "coop" | "")
    with_threads=qt
  ;;
  "no" | "null")
    with_threads=null
  ;;
  * )
    AC_MSG_ERROR(invalid value for --with-threads: $with_threads)
  ;;
esac
AC_MSG_RESULT($with_threads)

## Make sure the threads package we've chosen is actually supported on
## the present platform.
case "${with_threads}" in
  "qt" )
    ## This configures the QuickThreads package, and sets or clears
    ## the THREAD_PACKAGE variable if qthreads don't configure
    ## correctly.  In that case, we fall back on null-threads.
    QTHREADS_CONFIGURE
   ;;
  "null" )
    THREAD_PACKAGE="null"
   ;;
esac

## We always provide the thread API now and thus, USE_THREADS is
## always defined and threads.o is always included.

AC_DEFINE(USE_THREADS, 1, [Define if providing the thread API.])
AC_LIBOBJ([threads])

case "${THREAD_PACKAGE}" in
  "QT" )
    AC_DEFINE(USE_COOP_THREADS, 1,
      [Define if using cooperative multithreading.])

    AC_ARG_ENABLE(linuxthreads,
      [  --disable-linuxthreads  disable linuxthreads workaround],,
       enable_linuxthreads=yes)

    ## Workaround for linuxthreads (optionally disabled)
    if test $host_os = linux-gnu -a "$enable_linuxthreads" = yes; then
      AC_DEFINE(GUILE_PTHREAD_COMPAT, 1,
        [Define to enable workaround for COOP-linuxthreads compatibility.])
      AC_CHECK_LIB(pthread, main)
    fi

    ## Bring in scm_internal_select, if appropriate.
    if test $ac_cv_func_gettimeofday = yes &&
       test $ac_cv_func_select = yes; then
      AC_DEFINE(GUILE_ISELECT, 1, [Define to implement scm_internal_select.])
    fi

  ;;
  "null" | "" )
    AC_DEFINE(USE_NULL_THREADS, 1,
      [Define if using one-thread 'multi'threading.])
  ;;
  * )
    AC_MSG_ERROR(invalid value for THREAD_PACKAGE: ${THREAD_PACKAGE})
  ;;
esac


## Cross building	
if test "$cross_compiling" = "yes"; then
  AC_MSG_CHECKING(cc for build)
  ## /usr/bin/cc still uses wrong assembler
  ## CC_FOR_BUILD="${CC_FOR_BUILD-/usr/bincc}"
  CC_FOR_BUILD="${CC_FOR_BUILD-PATH=/usr/bin:$PATH cc}"
else
  CC_FOR_BUILD="${CC_FOR_BUILD-$CC}"
fi   
AC_ARG_WITH(cc-for-build,
  [  --with-cc-for-build=CC  native C compiler, to be used during build])
test -n "$with_cc_for_build" && CC_FOR_BUILD="$with_cc_for_build"
     
## AC_MSG_CHECKING("if we are cross compiling")
## AC_MSG_RESULT($cross_compiling)
if test "$cross_compiling" = "yes"; then
   AC_MSG_RESULT($CC_FOR_BUILD)
fi

## No need as yet to be more elaborate
CCLD_FOR_BUILD="$CC_FOR_BUILD"

AC_SUBST(cross_compiling)
AC_SUBST(CC_FOR_BUILD)
AC_SUBST(CCLD_FOR_BUILD)
	
## libtool erroneously calls CC_FOR_BUILD HOST_CC;
## --HOST is the platform that PACKAGE is compiled for.
HOST_CC="$CC_FOR_BUILD"
AC_SUBST(HOST_CC)

if test "$cross_compiling" = "yes"; then
  AC_MSG_CHECKING(guile for build)
  GUILE_FOR_BUILD="${GUILE_FOR_BUILD-guile}"
else
  GUILE_FOR_BUILD='$(preinstguile)'
fi   
AC_ARG_WITH(guile-for-build,
  [  --with-guile-for-build=guile  native guile executable, to be used during build])
test -n "$with_guile_for_build" && GUILE_FOR_BUILD="$with_guile_for_build"

## AC_MSG_CHECKING("if we are cross compiling")
## AC_MSG_RESULT($cross_compiling)
if test "$cross_compiling" = "yes"; then
   AC_MSG_RESULT($GUILE_FOR_BUILD)
fi
AC_SUBST(GUILE_FOR_BUILD)
  			
# Do this here so we don't screw up any of the tests above that might
# not be "warning free"

if test "${GUILE_ERROR_ON_WARNING}" = yes
then
  CFLAGS="${CFLAGS} -Werror"
  enable_compile_warnings=no
fi

## If we're using GCC, ask for aggressive warnings.
case "$GCC" in
  yes )
    ## We had -Wstrict-prototypes in here for a bit, but Guile does too
    ## much stuff with generic function pointers for that to really be
    ## less than exasperating.
    ## -Wpointer-arith was here too, but something changed in gcc/glibc
    ## and it became equally exasperating (gcc 2.95 and/or glibc 2.1.2).
    CFLAGS="$CFLAGS -Wall -Wmissing-prototypes" ;;
esac

## NOTE the code below sets LIBOBJS directly and so is now forbidden
## -- I'm disabling it for now in the hopes that the newer autoconf
## will DTRT -- if not, we need to fix up the sed command to match the
## others...
##
## Remove fileblocks.o from the object list.  This file gets added by
## the Autoconf macro AC_STRUCT_ST_BLOCKS.  But there is no need.
#LIBOBJS="`echo ${LIBOBJS} | sed 's/fileblocks\.o//g'`"

## If we're creating a shared library (using libtool!), then we'll
## need to generate a list of .lo files corresponding to the .o files
## given in LIBOBJS.  We'll call it LIBLOBJS.
LIBLOBJS="`echo ${LIB@&t@OBJS} | sed 's,\.[[^.]]* ,.lo ,g;s,\.[[^.]]*$,.lo,'`"

## We also need to create corresponding .doc and .x files
EXTRA_DOT_DOC_FILES="`echo ${LIB@&t@OBJS} | sed 's,\.[[^.]]* ,.doc ,g;s,\.[[^.]]*$,.doc,'`"
EXTRA_DOT_X_FILES="`echo ${LIB@&t@OBJS} | sed 's,\.[[^.]]* ,.x ,g;s,\.[[^.]]*$,.x,'`"

AC_SUBST(GUILE_MAJOR_VERSION)
AC_SUBST(GUILE_MINOR_VERSION)
AC_SUBST(GUILE_MICRO_VERSION)
AC_SUBST(GUILE_VERSION)

#######################################################################
# library versioning

AC_SUBST(LIBQTHREADS_INTERFACE_CURRENT)
AC_SUBST(LIBQTHREADS_INTERFACE_REVISION)
AC_SUBST(LIBQTHREADS_INTERFACE_AGE)
AC_SUBST(LIBQTHREADS_INTERFACE)

AC_SUBST(LIBGUILE_INTERFACE_CURRENT)
AC_SUBST(LIBGUILE_INTERFACE_REVISION)
AC_SUBST(LIBGUILE_INTERFACE_AGE)
AC_SUBST(LIBGUILE_INTERFACE)

AC_SUBST(LIBGUILE_SRFI_SRFI_4_INTERFACE_CURRENT)
AC_SUBST(LIBGUILE_SRFI_SRFI_4_INTERFACE_REVISION)
AC_SUBST(LIBGUILE_SRFI_SRFI_4_INTERFACE_AGE)
AC_SUBST(LIBGUILE_SRFI_SRFI_4_INTERFACE)

AC_SUBST(LIBGUILE_SRFI_SRFI_13_14_INTERFACE_CURRENT)
AC_SUBST(LIBGUILE_SRFI_SRFI_13_14_INTERFACE_REVISION)
AC_SUBST(LIBGUILE_SRFI_SRFI_13_14_INTERFACE_AGE)
AC_SUBST(LIBGUILE_SRFI_SRFI_13_14_INTERFACE)

#######################################################################

dnl Tell guile-config what flags guile users should link against.
GUILE_LIBS="$LDFLAGS $THREAD_LIBS_INSTALLED $LIBS"
AC_SUBST(GUILE_LIBS)

AC_SUBST(AWK)
AC_SUBST(LIBLOBJS)
AC_SUBST(EXTRA_DOT_DOC_FILES)
AC_SUBST(EXTRA_DOT_X_FILES)

dnl See also top_builddir in info node: (libtool)AC_PROG_LIBTOOL
top_builddir_absolute=`pwd`
AC_SUBST(top_builddir_absolute)
top_srcdir_absolute=`(cd $srcdir ; pwd)`
AC_SUBST(top_srcdir_absolute)

AC_CONFIG_FILES([
  Makefile
  am/Makefile
  libguile/Makefile
  libguile/guile-snarf
  libguile/guile-doc-snarf
  libguile/guile-func-name-check
  libguile/guile-snarf-docs
  libguile/version.h
  libguile-ltdl/Makefile
  libguile-ltdl/upstream/Makefile
  ice-9/Makefile
  lang/Makefile
  lang/elisp/Makefile
  lang/elisp/internals/Makefile
  lang/elisp/primitives/Makefile
  oop/Makefile
  oop/goops/Makefile
  scripts/Makefile
  srfi/Makefile
  qt/Makefile
  qt/qt.h
  qt/md/Makefile
  qt/time/Makefile
  guile-config/Makefile
  doc/Makefile
  doc/ref/Makefile
  doc/tutorial/Makefile
  doc/goops/Makefile
  doc/r5rs/Makefile
  examples/Makefile
  examples/scripts/Makefile
  examples/box/Makefile
  examples/box-module/Makefile
  examples/box-dynamic/Makefile
  examples/box-dynamic-module/Makefile
  examples/modules/Makefile
  examples/safe/Makefile
  test-suite/Makefile
  check-guile
  benchmark-suite/Makefile
  benchmark-guile
  guile-tools
  pre-inst-guile])

AC_CONFIG_COMMANDS(default,
  [ chmod +x libguile/guile-snarf \
             libguile/guile-doc-snarf \
             libguile/guile-func-name-check \
             libguile/guile-snarf-docs \
             check-guile \
             benchmark-guile \
             guile-tools \
             pre-inst-guile])

AC_OUTPUT

dnl Local Variables:
dnl comment-start: "dnl "
dnl comment-end: ""
dnl comment-start-skip: "\\bdnl\\b\\s *"
dnl End:
